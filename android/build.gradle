apply plugin: 'com.android.library'

group = 'expo.modules.nativesea'
version = '0.7.6'

def expoModulesCorePlugin = new File(project(":expo-modules-core").projectDir.absolutePath, "ExpoModulesCorePlugin.gradle")
apply from: expoModulesCorePlugin
applyKotlinExpoModulesCorePlugin()
useCoreDependencies()
useExpoPublishing()

// If you want to use the managed Android SDK versions from expo-modules-core, set this to true.
// The Android SDK versions will be bumped from time to time in SDK releases and may introduce breaking changes in your module code.
// Most of the time, you may like to manage the Android SDK versions yourself.
def useManagedAndroidSdkVersions = false
if (useManagedAndroidSdkVersions) {
  useDefaultAndroidSdkVersions()
} else {
  buildscript {
    // Simple helper that allows the root project to override versions declared by this library.
    ext.safeExtGet = { prop, fallback ->
      rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
    }
  }
  project.android {
    compileSdkVersion safeExtGet("compileSdkVersion", 36)
    defaultConfig {
      minSdkVersion safeExtGet("minSdkVersion", 24)
      targetSdkVersion safeExtGet("targetSdkVersion", 36)
    }
  }
}

android {
  namespace "expo.modules.nativesea"
  defaultConfig {
    versionCode 1
    versionName "0.7.6"
    externalNativeBuild {
      cmake {
        // CMake will be told where to find the unpacked Prefab via this argument
        arguments "-DOPENSSL_PREFAB_DIR=${project.buildDir}/intermediates/nativeSeaAar/prefab/modules/openssl"
      }
    }
  }
  lintOptions {
    abortOnError false
  }
}

repositories {
  mavenCentral()
  google()
}

dependencies {
  implementation 'com.madgag.spongycastle:core:1.58.0.0'
  implementation 'com.madgag.spongycastle:prov:1.54.0.0'
  implementation 'com.madgag.spongycastle:pg:1.54.0.0'
  // Provide React Native bridge types when compiling the module
  implementation "com.facebook.react:react-native:+"
}

// Unpack only the Prefab metadata from the shipped AAR so CMake can consume it.
def nativeSeaAar = file("${projectDir}/../node_modules/native-sea-openssl/android/native-sea-openssl-release.aar")
task unpackNativeSeaAar(type: Copy) {
  onlyIf { nativeSeaAar.exists() }
  from { zipTree(nativeSeaAar) }
  include "prefab/**"
  into "${buildDir}/intermediates/nativeSeaAar"
}
preBuild.dependsOn unpackNativeSeaAar

android.externalNativeBuild {
  cmake {
    path "src/main/cpp/CMakeLists.txt"
  }
}
